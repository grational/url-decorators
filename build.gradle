/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 19-02-2017 10.26
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

// [general] configuration
project.ext {
	groovyVersion   = '3.0.9'
	gradleVersion   = '7.2'
	spockVersion    = '2.0-groovy-3.0'
	slf4jVersion    = '1.7.32'
}

scmVersion {
	tag {
		prefix = 'release-'
	}
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group                 'it.grational'
	version               scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description           "A series of classes used to create and to manipulate URLs"
}

// [crosscompile] source and target java version
allprojects {
	sourceCompatibility = '1.8'
	targetCompatibility = sourceCompatibility
}

repositories {
	jcenter()
}

dependencies {
	// Groovy language support
	implementation "org.codehaus.groovy:groovy:$groovyVersion"

	// We use the awesome Spock testing and specification framework
	testImplementation "org.spockframework:spock-core:$spockVersion"
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'url-decorators'
			version = scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)

			from components.java
		}
	}
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

check {
	// clean before check
	dependsOn 'clean'
}
// vim: ft=groovy:fdm=indent
